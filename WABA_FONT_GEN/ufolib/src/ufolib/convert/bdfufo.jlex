package ufolib.convert;

import java_cup.runtime.*;
import ufolib.convert.*;
%%

%cup
%notunix
%unicode
%public
%class BdfScan
%eofval{
  return new Symbol( BUSym.EOF );
%eofval}
%state s_untileol,s_propname,s_propvalue,s_hexline,s_propcount,s_commentvalue

ws=[ \n\t]
digit=[0-9]
string=[^ \n\t]+
untileol=[^\n]+
iden=[^ \n\t]+
hexline=[0-9a-fA-F]+
number=-?{digit}+(\.{digit}+)?
integer=-?{digit}+

%%
<s_commentvalue> .*\n {yybegin( YYINITIAL );}
{ws} {}
<YYINITIAL> FONT {yybegin( s_untileol ); return new Symbol( BUSym.font );}
<YYINITIAL> SIZE {return new Symbol( BUSym.size );}
<YYINITIAL> FONTBOUNDINGBOX {return new Symbol( BUSym.fontboundingbox );}
<YYINITIAL> STARTFONT {return new Symbol( BUSym.startfont );}
<YYINITIAL> ENDFONT {return new Symbol( BUSym.endfont );}
<YYINITIAL> STARTPROPERTIES {yybegin( s_propcount ); return new Symbol( BUSym.startproperties );}
<s_propname> ENDPROPERTIES {yybegin( YYINITIAL ); return new Symbol( BUSym.endproperties );}
<YYINITIAL> CHARS {return new Symbol( BUSym.chars );}
<YYINITIAL> STARTCHAR {return new Symbol( BUSym.startchar );}
<YYINITIAL> ENCODING {return new Symbol( BUSym.encoding );}
<YYINITIAL> SWIDTH {return new Symbol( BUSym.swidth );}
<YYINITIAL> DWIDTH {return new Symbol( BUSym.dwidth );}
<YYINITIAL> BBX {return new Symbol( BUSym.bbx );}
<YYINITIAL> BITMAP {yybegin( s_hexline ); return new Symbol( BUSym.bitmap );}
<s_hexline,YYINITIAL> ENDCHAR {yybegin( YYINITIAL ); return new Symbol( BUSym.endchar );}
<YYINITIAL> COMMENT {yybegin( s_commentvalue );}

<YYINITIAL> {integer} {yybegin( YYINITIAL ); return new Symbol( BUSym.t_integer, new Integer( yytext() ) );}
<s_propcount> {integer} {yybegin( s_propname ); return new Symbol( BUSym.t_integer, new Integer( yytext() ) );}
<YYINITIAL> {number} {yybegin( YYINITIAL ); return new Symbol( BUSym.t_number, new Float( yytext() ) );}
<YYINITIAL,s_hexline> {iden} {return new Symbol( BUSym.t_iden, new String( yytext() ) );}
<s_propname> {iden} {yybegin( s_propvalue );return new Symbol( BUSym.t_iden, new String( yytext() ) );}
<s_propvalue> {untileol} {yybegin( s_propname );return new Symbol( BUSym.t_string, new String( yytext() ) );}

<s_untileol> {untileol} {yybegin( YYINITIAL ); return new Symbol( BUSym.t_string, new String( yytext() ) );}
. {System.out.println("Illegal character: <" + yytext() + ">");}



